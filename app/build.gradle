apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'

def mapboxApiToken = project.properties['MAPBOX_ACCESS_TOKEN'] ?: System.getenv('MAPBOX_ACCESS_TOKEN')
if (mapboxApiToken == null) {
    throw new Exception("API token is not specified.")
}

android {
    compileSdkVersion 30

    defaultConfig {
        buildConfigField "String", "MAPBOX_API_TOKEN", "\"$mapboxApiToken\""
        resValue "string", "mapbox_access_token", mapboxApiToken

        applicationId "com.mapbox.search.demo"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        allWarningsAsErrors = true
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'

    // Loader module is excluded just for custom library loader example.
    // Don't exclude it if you are good with a default loader.
    implementation ("com.mapbox.search:mapbox-search-android-ui:1.0.0-beta.24") {
        exclude group: "com.mapbox.common", module: "loader"
    }
    implementation ("com.mapbox.maps:android:10.2.0") {
        exclude group: "com.mapbox.common", module: "loader"
    }

    // Needed just for custom library loader example.
    compileOnly "com.mapbox.base:annotations:0.6.0"
    kapt "com.mapbox.base:annotations-processor:0.5.0"

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
